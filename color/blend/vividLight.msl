#include "colorBurn.msl"
#include "colorDodge.msl"

/*
contributors: Jamie Owen
description: Photoshop Vivid Light blend mode mplementations sourced from this article on https://mouaif.wordpress.com/2009/01/05/photoshop-math-with-glsl-shaders/
use: blendVividLight(<float|float3> base, <float|float3> blend [, <float> opacity])
license: MIT License (MIT) Copyright (c) 2015 Jamie Owen
*/

#ifndef FNC_BLENDVIVIDLIGHT
#define FNC_BLENDVIVIDLIGHT
float blendVividLight(float base, float blend) {
    return (blend < .5)? blendColorBurn(base, float(2.*blend)): blendColorDodge(base, float(2. * (blend - .5)));
}

float3 blendVividLight(float3 base, float3 blend) {
    return float3(blendVividLight(base.r, blend.r),
                            blendVividLight(base.g, blend.g),
                            blendVividLight(base.b, blend.b));
}

float3 blendVividLight(float3 base, float3 blend, float opacity) {
    return (blendVividLight(base, blend) * opacity + base * (1. - opacity));
}

half blendVividLight(half base, half blend) {
    return (blend < .5)? blendColorBurn(base, half(2.*blend)): blendColorDodge(base, half(2. * (blend - .5)));
}

half3 blendVividLight(half3 base, half3 blend) {
    return half3(blendVividLight(base.r, blend.r),
                            blendVividLight(base.g, blend.g),
                            blendVividLight(base.b, blend.b));
}

half3 blendVividLight(half3 base, half3 blend, half opacity) {
    return (blendVividLight(base, blend) * opacity + base * (1. - opacity));
}
#endif
